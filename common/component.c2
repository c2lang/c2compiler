/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module component;

import ast;
import ast_context;
import color;
import module_list;
import string_buffer;
import string_list;
import string_pool;

import stdlib local;
import string;
import stdio local;

/*
    Represents a library or the main-component

    Used to:
    - create + store Modules
*/

// Note: only for main component
public type Kind enum u8 {
    Image,
    Executable,
    StaticLibrary,
    DynamicLibrary,
    ExternalStatic,
    ExternalDynamic,
}

const char*[] kind_names = {
    "image",
    "executable",
    "static library",
    "dynamic library",
    "external static library",
    "external dynamic library",
}

static_assert(elemsof(Kind), elemsof(kind_names));

public type Component struct {
    u32 name_idx;   // into auxPool
    u32 dirname_idx;   // into auxPool
    u32 linkname;   // into auxPool
    Kind kind;
    bool is_direct; // only for external, if used directly (otherwise sub-dep)
    bool available_static;  // only for external
    bool available_dynamic; // only for external

    ast_context.Context* context;   // no ownership
    string_pool.Pool* auxPool;      // no ownership
    module_list.List* allmodules;   // no ownership

    module_list.List mods;
    string_list.List deps;
} @(opaque)

public func Component* create(ast_context.Context* context,
                              string_pool.Pool* auxPool,
                              module_list.List* allmodules,
                              u32 name_idx, // into auxPool
                              Kind kind,
                              bool is_direct)
{
    Component* c = calloc(1, sizeof(Component));
    c.name_idx = name_idx;
    c.kind = kind;
    c.is_direct = is_direct;
    c.context = context;
    c.auxPool = auxPool;
    c.allmodules = allmodules;

    c.mods.init(true);
    c.deps.init(auxPool);
    return c;
}

public func void Component.free(Component* c) {
    c.mods.free();
    c.deps.free();
    free(c);
}

public func const char* Component.getName(const Component* c) { return c.auxPool.idx2str(c.name_idx); }

public func u32 Component.getNameIdx(const Component* c) { return c.name_idx; }

public func void Component.setPath(Component* c, u32 dirname) { c.dirname_idx = dirname; }

public func const char* Component.getPath(const Component* c) { return c.auxPool.idx2str(c.dirname_idx); }

public func void Component.setLinkName(Component* c, const char* name) {
    c.linkname = c.auxPool.addStr(name, false);
}

public func const char* Component.getLinkName(const Component* c) {
    if (c.linkname) return c.auxPool.idx2str(c.linkname);
    return nil;
}

public func bool Component.isExternal(const Component* c) { return c.kind >= Kind.ExternalStatic; }

public func bool Component.isDirect(const Component* c) { return c.is_direct; }

public func void Component.setKind(Component* c, bool is_static, bool is_dynamic) {
    c.available_static = is_static;
    c.available_dynamic = is_dynamic;
}

public func bool Component.isStaticLib(const Component* c) {
    return c.kind == Kind.StaticLibrary || c.kind == Kind.ExternalStatic;
}

public func bool Component.isAvailableStatic(const Component* c) { return c.available_static; }

public func bool Component.isAvailableDynamic(const Component* c) { return c.available_dynamic; }

public func void Component.visitModules(const Component* c, module_list.Visitor visitor, void* arg) {
    c.mods.visit(visitor, arg);
}

public func module_list.List* Component.getModules(Component* c) { return &c.mods; }

public func ast.Module* Component.getOrAddModule(Component* c, u32 name_idx) {
    ast.Module* m = c.mods.find(name_idx);
    if (m) return m;

    return c.createModule(name_idx);
}

public func ast.Module* Component.createModule(Component* c, u32 name_idx) {
    ast.Module* m = c.allmodules.find(name_idx);
    if (m) return nil;

    m = ast.Module.create(c.context, name_idx, c.isExternal(), c.is_direct);

    c.mods.add(m);
    c.allmodules.add(m);
    return m;
}

public func bool Component.hasModule(const Component* c, const ast.Module* mod) {
    return c.mods.contains(mod);
}

public func void Component.print(const Component* c, bool show_funcs) {
    string_buffer.Buf* out = string_buffer.create(128*1024, color.useColor(), 1);
    const ast.Module** mods = c.mods.getConst();
    for (u32 i=0; i<c.mods.length(); i++) {
        mods[i].print(out, show_funcs);
    }

    out.color(color.Normal);
    stdio.puts(out.data());
    out.free();
}

public func void Component.printModules(const Component* c) {
    string_buffer.Buf* out = string_buffer.create(4096, color.useColor(), 1);
    out.print("--- %s [%s]", c.getName(), kind_names[c.kind]);
    if (!c.is_direct) out.add(" (indirect)");
    out.add(" ---\n");
    const ast.Module** mods = c.mods.getConst();
    for (u32 i=0; i<c.mods.length(); i++) {
        const ast.Module* m = mods[i];
        const char* col = m.isUsed() ? color.Normal : color.Grey;
        out.color(col);
        out.print("   %s\n", m.getName());
    }
    out.color(color.Normal);
    stdio.puts(out.data());
    out.free();
}

public func void Component.printSymbols(const Component* c, bool print_external) {
    if (c.isExternal() != print_external) return;
    string_buffer.Buf* out = string_buffer.create(4096, color.useColor(), 1);
    out.print("--- %s [%s] ---\n", c.getName(), kind_names[c.kind]);
    const ast.Module** mods = c.mods.getConst();
    for (u32 i=0; i<c.mods.length(); i++) {
        out.color(color.Cyan);
        out.print("  %s\n", mods[i].getName());
        const ast.SymbolTable* table = mods[i].getSymbols();
        table.print(out);
    }
    out.color(color.Normal);
    stdio.puts(out.data());
    out.free();
}

public func void Component.addDep(Component* c, const char* dep) {
    u32 dep_idx = c.auxPool.addStr(dep, true);
    c.deps.add(dep_idx);
}

public func string_list.List* Component.getDeps(Component* c) {
    return &c.deps;
}


public type List struct {
    Component** components;
    u32 count;
    u32 capacity;
}

public func void List.init(List* l) {
    l.components = nil;
    l.count = 0;
    l.resize(4);
}

public func void List.free(List* l) {
    for (u32 i=0; i<l.count; i++) {
        l.components[i].free();
    }
    free(cast<void*>(l.components));
}

func void List.resize(List* l, u32 capacity) {
    l.capacity = capacity;
    Component** comps2 = malloc(l.capacity * sizeof(Component*));
    if (l.count) {
        string.memcpy(cast<void*>(comps2), cast<void*>(l.components), l.count * sizeof(Component*));
        free(cast<void*>(l.components));
    }
    l.components = comps2;
}

public func void List.add(List* l, Component* c) {
    if (l.count == l.capacity) l.resize(l.capacity * 2);

    l.components[l.count] = c;
    l.count++;
}

public func u32 List.size(const List* l) {
    return l.count;
}

public func Component* List.get(List* l, u32 idx) {
    return l.components[idx];
}

public func Component** List.get_all(List* l) {
    return l.components;
}

public func bool Component.isLibrary(const Component* c) {
    return c.kind == Kind.StaticLibrary || c.kind == Kind.DynamicLibrary;
}

public func Kind Component.getKind(const Component* c) {
    return c.kind;
}

