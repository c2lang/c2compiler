/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module build_target;

import stdlib;
import string;

type Lib struct {
    u32 name; // into auxPool
    Kind kind;
    // TODO src_loc (of recipe/manifest?)
}

type List struct {
    Lib* libs;
    u32 count;
    u32 capacity;
}

fn void List.init(List* l) {
    string.memset(l, 0, sizeof(List));
    l.resize(2);
}

fn void List.free(List* l) {
    stdlib.free(l.libs);
}

fn void List.visit(const List* l, Visitor visitor, void* arg) {
    for (u32 i=0; i<l.count; i++) {
        const Lib* lib = &l.libs[i];
        visitor(arg, lib.name, lib.kind);
    }
}

fn void List.add(List* l, u32 name, Kind kind) {
    // Note: we dont check for duplicates here
    if (l.count == l.capacity) l.resize(l.capacity * 2);
    l.libs[l.count].name = name;
    l.libs[l.count].kind = kind;
    l.count++;
}

fn bool List.contains(const List* l, u32 name) {
    for (u32 i=0; i<l.count; i++) {
        if (l.libs[i].name == name) return true;
    }
    return false;

}

//public fn u32 List.size(const List* l) { return l.count; }

fn void List.resize(List* l, u32 cap) {
    l.capacity = cap;
    Lib* libs2 = stdlib.malloc(l.capacity * sizeof(Lib));
    if (l.libs) {
        string.memcpy(libs2, l.libs, l.count * sizeof(Lib));
        stdlib.free(l.libs);
    }
    l.libs = libs2;
}

