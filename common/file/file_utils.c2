/* Copyright 2022-2024 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module file_utils;

import c_errno local;
import libc_fcntl local;
import string;
import sys_stat local;
import unistd local;

fn const char* find_char(const char* full, char delim) {
    while (1) {
        char c = *full;
        if (c == delim) return full;
        if (c == 0) break;
        full++;
    }
    return nil;
}


// return 0 on succes, -1 otherwise
fn i32 create_dir(const char* path, bool follow) {
    i32 err = mkdir(path, 0777);
    if (err && *errno2() != EEXIST) return -1;

    if (!follow) return 0;

    i32 fd = openat(AT_FDCWD, path, O_RDONLY|O_NOCTTY|O_NONBLOCK|O_NOFOLLOW|O_DIRECTORY);
    if (fd == -1) return *errno2();

    err = fchdir(fd);
    if (err == -1) return -1;

    close(fd);
    return 0;
}

// returns 0 on success, errno on failure
public fn i32 create_directory(const char* path) {
    i32 fd = openat(AT_FDCWD, ".", O_RDONLY);
    if (fd == -1) return *errno2();

    // TODO handle . / .., empty path
    char[128] tmp;
    const char* cp = path;
    i32 err = 0;
    while (*cp) {
        const char* slash = find_char(cp, '/');
        // TODO check for empty string (//)
        if (slash) {
            usize len = cast<usize>(slash - cp);
            string.memcpy(tmp, cp, len);
            tmp[len] = 0;
            cp = slash + 1;
            err = create_dir(tmp, true);
            if (err != 0) break;
        } else {
            err = create_dir(cp, false);
            break;
        }
    }
    i32 errno_ = 0;
    if (err) errno_ = *errno2();

    fchdir(fd);
    close(fd);
    return errno_;
}

public fn bool exists(const char* filename) {
    Stat statbuf;
    // TODO also check regular file
    return (stat(filename, &statbuf) == 0);
}

