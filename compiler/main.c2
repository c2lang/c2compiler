/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c2c_main;

import build_file;
import build_target;
import color;
import console;
import c2recipe;
import compiler;
import constants;
import file_utils;
#if C2_PLUGINS
import git_version; // from plugin
#endif
import diagnostics;
import plugin_info;
import plugin_mgr;
import source_mgr;
import string_list;
import string_buffer;
import string_pool;
import string_utils;
import utils;

import stdio;
import stdlib local;
import unistd;
import string local;
import c_errno local;

type Options struct {
    bool log_verbose;
    bool force_warnings;
    bool print_timing;
    bool show_targets;
    bool show_plugins;
    bool no_plugins;
    bool use_qbe_backend;
    bool use_ir_backend;
    const char* target;
    const char* single_file;
    const char* build_file;
    const char* other_dir;
    string_list.List targets;
    string_list.List files;
}

fn void Options.init(Options *opts, string_pool.Pool* pool) {
    memset(opts, 0, sizeof(Options));
    opts.targets.init(pool);
    opts.files.init(pool);
}

fn void Options.free(Options *opts) {
    opts.targets.free();
    opts.files.free();
}

fn void create_project(const char* name) {

    if (file_utils.exists("main.c2") || file_utils.exists("recipe.txt")) {
        console.error("c2c: main.c2 and/or recipe.txt already exist");
        exit(EXIT_FAILURE);
    }

    string_buffer.Buf* buf = string_buffer.create(4096, false, 2);
    file_utils.Writer writer;

    buf.print("module %s_main;\n\n", name);
    buf.add("public fn i32 main(i32 argc, char** argv) {\n");
    buf.add("\n");
    buf.add("\treturn 0;\n");
    buf.add("}\n");
    bool ok = writer.write("main.c2", buf.udata(), buf.size());
    if (!ok) {
        console.error("c2c: cannot write to %s: %s", "main.c2", writer.getError());
        exit(EXIT_FAILURE);
    }

    buf.clear();
    buf.add("plugin deps_generator [all-targets files private]\n");
    buf.add("plugin refs_generator [all-targets]\n");
    buf.newline();
    buf.print("\nexecutable %s\n", name);
    buf.add("\t$warnings no-unused\n");
    buf.add("\t$backend c\n");
    buf.newline();
    buf.add("\tmain.c2\n");
    buf.add("end\n");
    ok = writer.write("recipe.txt", buf.udata(), buf.size());
    if (!ok) {
        console.error("c2c: cannot write to %s: %s", "recipe.txt", writer.getError());
        exit(EXIT_FAILURE);
    }

    buf.free();
    console.log("created new project files");
    exit(EXIT_SUCCESS);
}

const char[] Recipe_help =
    "---- recipe.txt ----\n"
    "\n"
    "plugin <name> [<plugin-options>]\n"
    "\n"
    "config <options>\n"
    "\n"
    "set <name>\n"
    "   <files>\n"
    "end\n"
    "\n"
    "executable <name>\n"
    "   $warnings <no-unused>\n"
    "             <no-unused-variable>\n"
    "             <no-unused-function>\n"
    "             <no-unused-parameter>\n"
    "             <no-unused-type>\n"
    "             <no-unused-module>\n"
    "             <no-unused-import> \n"
    "             <no-unused-public>\n"
    "             <no-unused-label> \n"
    "             <no-unused-enum-constant>\n"
    "             <promote-to-error> \n"
    "   $backend [c|qbe] <check>\n"
    "              <fast>\n"
    "              <no-build>\n"
    "   $nolibc\n"
    "   $disable-asserts\n"
    "   $config <options>\n"
    "   $plugin <name> [<plugin-options>]\n"
    "   $use <library-name> dynamic/static\n"
    "   (set-name)\n"
    "   <file1.c2>\n"
    "   <file2.c2>\n"
    "end\n"
    "\n"
    "lib <name> dynamic/static\n"
    "   $export <module-names>\n"
    "   other options same as executable\n"
    "end\n"
    "\n"
    "--------------------\n";

fn void print_recipe_help() {
    console.log(Recipe_help);
}

fn void print_version() {
#if C2_PLUGINS
    console.log("version: %s", git_version.Describe);
#else
    console.log("version: unknown");
#endif
}

const char[] Usage_help =
    "Usage: c2c <options> [<targets>]\n"
    "       c2c <options> <filenames>\n"
    "Options:\n"
    "\t-a                print ASTs\n"
    "\t-A                print Library ASTs\n"
    "\t-b [file]         use specified build file\n"
    "\t-d [dir]          change to [dir] first\n"
    //"\t-f [file]         only parse+build single file (in output/dummy/)\n"
    "\t-h                print this help\n"
    "\t-i                use IR backend\n"
    "\t-I                use IR backend and print generated IR\n"
    "\t-m                print modules\n"
    "\t-q                use QBE backend (EXPERIMENTAL, only for single files)\n"
    "\t-Q                use QBE backend and print generated QBE code\n"
    "\t-r                print reports\n"
    "\t-s                print symbols\n"
    "\t-S                print library symbols\n"
    "\t-t                print timings\n"
    "\t-T                print AST statistics\n"
    "\t-v                verbose logging\n"
    "\t-w                enable all warnings (overrides recipe)\n"
    "\t--asan            generate code with address sanity checks\n"
    "\t--msan            generate code with memory sanity checks\n"
    "\t--ubsan           generate code with undefined behavior sanity checks\n"
    "\t--check           only parse and check\n"
    "\t--create [name]   create an empty project recipe.txt and main.c2\n"
    "\t--fast            do fast, un-optimized build\n"
    "\t--help            print this help\n"
    "\t--help-recipe     print the recipe syntax\n"
    "\t--noplugins       do not use plugins\n"
    "\t--showlibs        print available libraries\n"
    "\t--showplugins     print available plugins\n"
    "\t--targets         show available targets in recipe\n"
    "\t--test            test mode (do not check for main() function)\n"
    "\t--version         print version\n";

fn void usage() {
    console.log(Usage_help);
    exit(EXIT_FAILURE);
}

fn void missing_arg(const char* option) {
    console.error("c2c: missing argument for option '%s'", option);
    exit(EXIT_FAILURE);
}

fn i32 parse_long_opt(i32 i, i32 argc, char** argv, compiler.Options* comp_opts, Options* opts) {
    const char* arg = argv[i];
    switch (arg+2) {
    case "check":
        comp_opts.check_only = true;
        break;
    case "create":
        if (i==argc-1) missing_arg(arg);
        i++;
        create_project(argv[i]);
        break;
    case "fast":
        comp_opts.fast_build = true;
        break;
    case "help":
        usage();
        break;
    case "help-recipe":
        print_recipe_help();
        exit(EXIT_SUCCESS);
    case "noplugins":
        opts.no_plugins = true;
        break;
    case "showlibs":
        comp_opts.show_libs = true;
        break;
    case "showplugins":
        opts.show_plugins = true;
        break;
    case "targets":
        opts.show_targets = true;
        break;
    case "test":
        comp_opts.test_mode = true;
        break;
    case "asan":
        comp_opts.asan = true;
        break;
    case "msan":
        comp_opts.msan = true;
        break;
    case "ubsan":
        comp_opts.ubsan = true;
        break;
    case "version":
        print_version();
        exit(EXIT_SUCCESS);
    default:
        console.error("c2c: unknown option: %s", arg);
        exit(EXIT_FAILURE);
    }
    return i;
}

fn void parse_opts(i32 argc, char** argv, compiler.Options* comp_opts, Options* opts) {
    for (i32 i=1; i<argc; i++) {
        const char* arg = argv[i];
        if (arg[0] == '-') {
            if (arg[1] == '-') {
                i = parse_long_opt(i, argc, argv, comp_opts, opts);
            } else {
                if (strlen(arg) != 2) {
                    console.error("c2c: unknown option '%s'", arg);
                    exit(EXIT_FAILURE);
                }
                switch (arg[1]) {
                case '0':    // 'hidden' feature, print AST early after parsing, then quit
                    comp_opts.print_ast_early = true;
                    break;
                case 'A':
                    comp_opts.print_lib_ast = true;
                    break;
                case 'I':
                    opts.use_ir_backend = true;
                    comp_opts.print_ir = true;
                    break;
                case 'Q':
                    opts.use_qbe_backend = true;
                    comp_opts.print_ir = true;
                    break;
                case 'S':
                    comp_opts.print_external_symbols = true;
                    break;
                case 'T':
                    comp_opts.print_ast_stats = true;
                    break;
                case 'a':
                    comp_opts.print_ast = true;
                    break;
                case 'b':
                    if (i==argc-1) missing_arg(arg);
                    i++;
                    opts.build_file = argv[i];
                    break;
                case 'd':
                    if (i==argc-1) missing_arg(arg);
                    i++;
                    opts.other_dir = argv[i];
                    break;
                case 'f':
                    if (i==argc-1) missing_arg(arg);
                    i++;
                    opts.files.addStr(argv[i]);
                    break;
                case '?':
                case 'h':
                    usage();
                    break;
                case 'i':
                    opts.use_ir_backend = true;
                    break;
                case 'm':
                    comp_opts.print_modules = true;
                    break;
                case 'q':
                    opts.use_qbe_backend = true;
                    break;
                case 'r':
                    comp_opts.print_reports = true;
                    break;
                case 's':
                    comp_opts.print_symbols = true;
                    break;
                case 't':
                    opts.print_timing = true;
                    break;
                case 'v':
                    opts.log_verbose = true;
                    break;
                case 'w':
                    opts.force_warnings = true;
                    break;
                default:
                    console.error("c2c: unknown option '-%c'", arg[1]);
                    exit(EXIT_FAILURE);
                    break;
                }
            }
        } else {
            if (string_utils.endsWith(arg, ".c2"))
                opts.files.addStr(arg);
            else
                opts.targets.addStr(arg);
        }
    }
    u32 num_targets = opts.targets.length();
    u32 num_files = opts.files.length();
    if (num_targets > 1 && num_files) {
        console.error("c2c: multiple targets cannot be combined with -f");
        exit(EXIT_FAILURE);
    }
    if (opts.use_qbe_backend && num_files != 1) {
        console.error("c2c: option -q can only be used for a single file");
        exit(EXIT_FAILURE);
    }
    if (opts.use_qbe_backend && opts.use_ir_backend) {
        console.error("only one backend can be selected");
        exit(EXIT_FAILURE);
    }
}

fn void plugins_start_target(void* arg, plugin_info.Info* info) {
    plugin_mgr.Mgr* plugins = arg;
    plugins.beginTarget(info);
}

fn void plugins_after_parsing(void* arg) {
    plugin_mgr.Mgr* plugins = arg;
    plugins.postParse();
}

fn void plugins_after_analysis(void* arg) {
    plugin_mgr.Mgr* plugins = arg;
    plugins.postAnalysis();
}

fn void plugins_end_target(void* arg) {
    plugin_mgr.Mgr* plugins = arg;
    plugins.endTarget();
}

public fn i32 main(i32 argc, char** argv) {
    console.init();

    utils.PathInfo path_info = {}

    // note: auxPool is used by recipe, build-file and manifests
    string_pool.Pool* auxPool = string_pool.create(32*1024, 256);
    source_mgr.SourceMgr* sm = source_mgr.create(auxPool, constants.Max_open_files);
    diagnostics.Diags* diags = diagnostics.create(sm, color.useColor(), &path_info);
    c2recipe.Recipe* recipe = c2recipe.create(sm, auxPool);

    i32 recipe_id = -1;
    bool hasError = false;

    Options opts;
    opts.init(auxPool);
    compiler.Options comp_opts = { }

    parse_opts(argc, argv, &comp_opts, &opts);

    console.setTiming(opts.print_timing);
    console.setDebug(opts.log_verbose);

    if (opts.other_dir) {
        if (unistd.chdir(opts.other_dir)) {
            console.error("c2c: cannot chdir to %s: %s", opts.other_dir, strerror(errno));
            exit(EXIT_FAILURE);
        }
    }

    if (opts.files.length()) {
        // TODO: specify target using -o or use first filename base as target name
        u32 target_name = opts.targets.length() ?
            opts.targets.get_idx(0) :
            auxPool.addStr("dummy", true);
        build_target.BackEndKind backend = build_target.BackEndKind.C;
        if (opts.use_qbe_backend) backend = build_target.BackEndKind.QBE;
        if (opts.use_ir_backend) backend = build_target.BackEndKind.IR;
        build_target.Target* t = recipe.addTarget(target_name, 0, build_target.Kind.Executable);
        t.setBackEnd(backend);
        for (u32 i = 0; i < opts.files.length(); i++)
            t.addFile(opts.files.get_idx(i), 0);
        t.disableWarnings();
    } else {
        if (!utils.findProjectDir(&path_info)) {
            console.error("c2c: error: cannot find project root directory\n"
                          "     c2c requires a %s file in the project root,\n"
                          "     Use argument -h for c2c usage and a list of available options",
                          constants.recipe_name);
            return -1;
        }
        u32 recipe_idx = auxPool.addStr(constants.recipe_name, false);

        i32 yaml_id = sm.open(recipe_idx, 0, false);
        if (yaml_id == -1) return -1;
        if (!recipe.parse(yaml_id)) return -1;

        // TEMP also parse yaml version
        recipe_id = yaml_id;
        //u32 recipe2_idx = auxPool.addStr("c2recipe.yaml", false);
        //recipe_id = sm.open(recipe2_idx, 0, false);
        //if (recipe_id == -1) return -1;
        //if (!recipe.parseYaml(recipe_id)) return -1;
    }

    // look for build file (or use specified one)
    if (!opts.build_file) {
        opts.build_file = utils.findBuildFile();
    }

    plugin_mgr.Mgr* plugins = plugin_mgr.create(auxPool, opts.print_timing, opts.log_verbose, opts.no_plugins);

    build_file.Info* build_info = nil;
    if (opts.build_file) {
        console.log("using build-file %s", opts.build_file);
        build_info = build_file.parse(sm, auxPool, opts.build_file);
        if (!build_info) return -1;

        const string_list.List* plugin_dirs = build_info.getPluginDirs();
        for (u32 i=0; i<plugin_dirs.length(); i++) {
            plugins.addPath(plugin_dirs.get_idx(i));
        }

        if (!opts.show_plugins) {
            // load global plugins from build-file
            for (u32 i=0; i<build_info.getNumPlugins(); i++) {
                const build_file.Plugin* p = build_info.getPlugin(i);
                if (!plugins.loadGlobal(p.name, p.options)) {
                    source_mgr.Location loc = sm.getLocation(p.loc);
                    char[256] loc_str;
                    stdio.sprintf(loc_str, "%s:%d:%d", loc.filename, loc.line, loc.column);
                    console.error_diag(loc_str, "%s", plugins.getError());
                    return -1;
                }
            }
        }
    } else {
        const char* plugin_dir = getenv("C2_PLUGINDIR");
        if (plugin_dir) {
            u32 path_name = auxPool.addStr(plugin_dir, true);
            plugins.addPath(path_name);
        }
    }

    if (opts.show_plugins) {
        plugins.show();
        return 0;
    }

    // load global plugins from recipe
    const build_target.PluginList* pl = recipe.getPlugins();
    for (u32 i=0; i<pl.size(); i++) {
        const build_target.Plugin* p = pl.get(i);
        if (!plugins.loadGlobal(p.name, p.options)) {
            source_mgr.Location loc = sm.getLocation(p.loc);
            char[256] loc_str;
            stdio.sprintf(loc_str, "%s:%d:%d", loc.filename, loc.line, loc.column);
            console.error_diag(loc_str, "%s", plugins.getError());
            return -1;
        }
    }

    const char* libdir = getenv("C2_LIBDIR");
    if (libdir) comp_opts.libdir = auxPool.addStr(libdir, true);
    if (!libdir && !opts.build_file) {
        console.warn("environment variable C2_LIBDIR not set!");
    }

    u32 plugins_feature = auxPool.addStr("C2_PLUGINS", false);

    compiler.PluginHandler pluginHandler = {
        .start_target = plugins_start_target,
        .after_parsing = plugins_after_parsing,
        .after_analysis = plugins_after_analysis,
        .end_target = plugins_end_target,
        .arg = plugins,
    }
    bool has_filter = (opts.targets.length() != 0);
    for (u32 i=0; i<recipe.numTargets(); i++) {
        build_target.Target* target = recipe.getTarget(i);
        if (opts.force_warnings) target.enableWarnings();
        u32 target_idx = target.getNameIdx();
        const char* target_name = auxPool.idx2str(target_idx);
        if (has_filter) {
            if (!opts.targets.contains_idx(target_idx))
                continue;
            opts.targets.del(target_idx);
        }
        if (opts.show_targets) {
            console.log("%s", target_name);
            continue;
        }
        console.log("building %s", target_name);

        // load target-specific plugins from recipe
        pl = target.getPlugins();
        for (u32 j=0; j<pl.size(); j++) {
            const build_target.Plugin* p = pl.get(j);
            if (!plugins.loadLocal(p.name, p.options)) {
                source_mgr.Location loc = sm.getLocation(p.loc);
                char[256] loc_str;
                stdio.sprintf(loc_str, "%s:%d:%d", loc.filename, loc.line, loc.column);
                console.error_diag(loc_str, "%s", plugins.getError());
                continue;
            }
        }

        if (!opts.no_plugins) target.addFeature(plugins_feature);
        if (opts.use_ir_backend) target.setBackEnd(build_target.BackEndKind.IR);

        if (!target.hasBackEnd()) {
            if (!comp_opts.test_mode && !comp_opts.check_only) {
                console.error("c2c: no backend for target %s defined in %s", target_name, constants.recipe_name);
                if (recipe_id != -1) sm.clear(recipe_id);
                continue;
            }
        }

        compiler.build(auxPool, sm, diags, build_info, target, &comp_opts, &pluginHandler);
        hasError |= (diags.getNumErrors() != 0);

        // TODO fix build_file
        if (recipe_id != -1) sm.clear(recipe_id);
    }

    if (has_filter && opts.targets.length()) {
        for (u32 i = 0; i < opts.targets.length(); i++) {
            const char* name = opts.targets.get(i);
            console.warn("no such target in %s: %s", constants.recipe_name, name);
        }
    }

    plugins.free();
    if (build_info) build_info.free();
    recipe.free();
    opts.free();
    auxPool.free();
    diags.free();
    sm.free();

    return hasError ? -1 : 0;
}

