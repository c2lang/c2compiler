/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module plugin_main;

import ast;
import component;
import console;
import constants;
import plugin_info;
import process_utils;

import stdlib local;
import string local;

public plugin_info.Plugin handle @(export) = {
    .load = load,
    .unload = unload,
    .init = init,
    .post_parse = nil,
    .post_analysis = nil,
    .name = "Git Version 1.0",
}

type Plugin struct {
    plugin_info.Info* info;
    char[128] version;
}

fn void* load(const char* options, bool show_timing, bool show_debug) {
    Plugin* p = calloc(1, sizeof(Plugin));
    console.init();
    console.setTiming(show_timing);
    console.setDebug(show_debug);
    // TODO handle options
    strcpy(p.version, "unknown");

    i32 retval = process_utils.run2(".", "git", "describe --tags --always --dirty", p.version);
    if (retval == 0) {
        console.debug("gitversion: version '%s'", p.version);
    } else {
        console.warn("gitversion: no git archive?");
    }

    return p;
}

fn void unload(void* arg) {
    free(arg);
}

fn void init(void* arg, plugin_info.Info* info) {
    Plugin* p = arg;
    p.info = info;
    ast.setGlobals(info.ast_globals);
    ast.builtins = info.ast_builtins;

    // create module git_version
    u32 mod_name = info.astPool.addStr("git_version", true);
    component.Component* mainComp = info.components.get(info.components.size()-1);
    ast.Module* mod = mainComp.createModule(mod_name);
    if (!mod) {
        console.error("git_version: module 'git_version' already exists");
        exit(EXIT_FAILURE);
    }

    // add AST to module
    ast.AST* a = mod.add(info.auxPool, info.auxPool.addStr("<generated>", false), true);

    // add equivalent of: const char[] Describe = "version";
    u32 var_name = info.astPool.addStr("Describe", true);

    // create type
    ast.TypeRefHolder ref;
    ref.init();
    ref.setConst();
    ref.addArray(nil);
    ref.setBuiltin(ast.BuiltinKind.Char, 0);

    // create variable
    u32 version = info.astPool.addStr(p.version, false);
    ast.StringLiteral* str = ast.StringLiteral.create(info.context, 0, version, cast<u32>(strlen(p.version))+1);
    ast.Expr* initExpr = cast<ast.Expr*>(str);
    ast.VarDecl* vd = ast.VarDecl.create(info.context, ast.VarDeclKind.GlobalVar, var_name, 0, true, &ref, a.getIdx(), 0, initExpr);
    ast.Decl* d = vd.asDecl();

    // add to ast + module-symbols
    a.addVarDecl(d);
    mod.addSymbol(var_name, d);
}

