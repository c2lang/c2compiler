/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import src_loc local;
import string_buffer;

type AssertStmtBits struct {
    u32 : NumStmtBits;
    u32 is_pointer : 1;
}

public type AssertStmt struct @(opaque) {
    Stmt parent;
    SrcLoc loc;
    Expr* inner;
}

public fn AssertStmt* AssertStmt.create(ast_context.Context* c,
                                          SrcLoc loc,
                                          Expr* inner)
{
    AssertStmt* s = c.alloc(sizeof(AssertStmt));
    s.parent.init(StmtKind.Assert);
    s.loc = loc;
    s.inner = inner;
#if AstStatistics
    Stats.addStmt(StmtKind.Assert, sizeof(AssertStmt));
#endif
    return s;
}

fn Stmt* AssertStmt.instantiate(AssertStmt* s, Instantiator* inst) {
    AssertStmt* s2 = AssertStmt.create(inst.c, s.loc, s.inner.instantiate(inst));
    return cast<Stmt*>(s2);
}

public fn SrcLoc AssertStmt.getLoc(const AssertStmt* s) { return s.loc; }

public fn Expr* AssertStmt.getInner(const AssertStmt* s) { return s.inner; }

public fn Expr** AssertStmt.getInner2(AssertStmt* s) { return &s.inner; }

public fn void AssertStmt.setPointer(AssertStmt* s) {
    s.parent.assertStmtBits.is_pointer = 1;
}

public fn bool AssertStmt.isPointer(const AssertStmt* s) {
    return s.parent.assertStmtBits.is_pointer;
}

fn void AssertStmt.print(const AssertStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    out.newline();
    s.inner.print(out, indent + 1);
}

