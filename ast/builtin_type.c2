/* Copyright 2022-2026 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;

// Note: dont change positions, since they are used to check interval
public type BuiltinKind enum u8 {
    Char,
    Int8,
    Int16,
    Int32,
    Int64,
    UInt8,
    UInt16,
    UInt32,
    UInt64,
    Float32,
    Float64,
    ISize,
    USize,
    Bool,
}

const char*[BuiltinKind] builtinType_names = {
    [Char] = "char",
    [Int8] = "i8",
    [Int16] = "i16",
    [Int32] = "i32",
    [Int64] = "i64",
    [UInt8] = "u8",
    [UInt16] = "u16",
    [UInt32] = "u32",
    [UInt64] = "u64",
    [Float32] = "f32",
    [Float64] = "f64",
    [ISize] = "isize",
    [USize] = "usize",
    [Bool] = "bool",
}

const bool[BuiltinKind] BuiltinType_promotable = {
    [Char] = true,
    [Int8] = true,
    [Int16] = true,
    [Int32] = false,
    [Int64] = false,
    [UInt8] = true,
    [UInt16] = true,
    [UInt32] = false,
    [UInt64] = false,
    [Float32] = false,
    [Float64] = false,
    [ISize] = false,
    [USize] = false,
    [Bool] = true,
}

const bool[BuiltinKind] BuiltinType_signed = {
    [Char] = false,
    [Int8] = true,
    [Int16] = true,
    [Int32] = true,
    [Int64] = true,
    [UInt8] = false,
    [UInt16] = false,
    [UInt32] = false,
    [UInt64] = false,
    [Float32] = true,
    [Float64] = true,
    [ISize] = true,
    [USize] = false,
    [Bool] = false,
}

const bool[BuiltinKind] BuiltinType_unsigned = {
    [Char] = true,
    [Int8] = false,
    [Int16] = false,
    [Int32] = false,
    [Int64] = false,
    [UInt8] = true,
    [UInt16] = true,
    [UInt32] = true,
    [UInt64] = true,
    [Float32] = false,
    [Float64] = false,
    [ISize] = false,
    [USize] = true,
    [Bool] = false,
}

const bool[BuiltinKind] BuiltinType_integer = {
    [Char] = true,
    [Int8] = true,
    [Int16] = true,
    [Int32] = true,
    [Int64] = true,
    [UInt8] = true,
    [UInt16] = true,
    [UInt32] = true,
    [UInt64] = true,
    [Float32] = false,
    [Float64] = false,
    [ISize] = true,
    [USize] = true,
    [Bool] = false,
}

// Default builtin type sizes (adjusted for 32-bit in globals)
const u32[BuiltinKind] BuiltinType_default_sizes = {
    [Char] = 1,
    [Int8] = 1,
    [Int16] = 2,
    [Int32] = 4,
    [Int64] = 8,
    [UInt8] = 1,
    [UInt16] = 2,
    [UInt32] = 4,
    [UInt64] = 8,
    [Float32] = 4,
    [Float64] = 8,
    [ISize] = 8,       // adjusted in global
    [USize] = 8,       // adjusted in global
    [Bool] = 1,
}

// Default builtin type widths (adjusted for 32-bit in globals)
const u32[BuiltinKind] BuiltinType_default_widths = {
    [Char] = 8,
    [Int8] = 8,
    [Int16] = 16,
    [Int32] = 32,
    [Int64] = 64,
    [UInt8] = 8,
    [UInt16] = 16,
    [UInt32] = 32,
    [UInt64] = 64,
    [Float32] = 0,
    [Float64] = 0,
    [ISize] = 64,       // adjusted in global
    [USize] = 64,       // adjusted in global
    [Bool] = 1,
}

public fn const char* BuiltinKind.str(BuiltinKind kind) {
    return builtinType_names[kind];
}

type BuiltinTypeBits struct {
    u32 : NumTypeBits;
    u32 kind : 4;
}

public type BuiltinType struct @(opaque) {
    Type base;
}

fn BuiltinType* BuiltinType.create(ast_context.Context* c, BuiltinKind kind) {
    BuiltinType* b = c.alloc(sizeof(BuiltinType));
    b.base.init(Builtin);
    b.base.builtinTypeBits.kind = kind;
    b.base.setCanonicalType(QualType.create(&b.base));
#if AstStatistics
    Stats.addType(Builtin, sizeof(BuiltinType));
#endif
    return b;
}

public fn BuiltinKind BuiltinType.getKind(const BuiltinType* b) {
    return (BuiltinKind)b.base.builtinTypeBits.kind;
}

// converts ISize/USize to Int64/UInt64 or Int32/UInt32 depending on arch
public fn BuiltinKind BuiltinType.getBaseKind(const BuiltinType* b) {
    return globals.builtinType_baseTypes[b.getKind()];
}

 fn BuiltinKind BuiltinType.getBuiltinKind(const BuiltinType* b) {
    return (BuiltinKind)b.base.builtinTypeBits.kind;
}

public fn bool BuiltinType.isInt32(const BuiltinType* b) {
    return b.getBuiltinKind() == Int32;
}

fn bool BuiltinType.isBool(const BuiltinType* b) {
    return b.getBuiltinKind() == Bool;
}

public fn const char* BuiltinType.kind2str(const BuiltinType* b) {
    return builtinType_names[b.getKind()];
}

public fn bool BuiltinType.isPromotableIntegerType(const BuiltinType* b) {
    return BuiltinType_promotable[b.getKind()];
}

public fn bool BuiltinType.isInteger(const BuiltinType* b) {
    return BuiltinType_integer[b.getKind()];
}

public fn bool BuiltinType.isIntegerOrBool(const BuiltinType* b) {
    return b.isInteger() || b.getKind() == Bool;
}

public fn bool BuiltinType.isFloatingPoint(const BuiltinType* b) {
    return (b.getKind() == Float32 || b.getKind() == Float64);
}

public fn bool BuiltinType.isSigned(const BuiltinType* b) {
    return BuiltinType_signed[b.getKind()];
}

public fn bool BuiltinType.isUnsigned(const BuiltinType* b) {
    return BuiltinType_unsigned[b.getKind()];
}

public fn u32 BuiltinType.getAlignment(const BuiltinType* b) {
    return globals.builtinType_sizes[b.getKind()];
}

public fn u32 BuiltinType.getWidth(const BuiltinType* b) {
    return globals.builtinType_width[b.getKind()];
}

fn void BuiltinType.print(const BuiltinType* b, string_buffer.Buf* out) {
    out.add(builtinType_names[b.getKind()]);
}

fn void BuiltinType.fullPrint(const BuiltinType* t, string_buffer.Buf* out, u32 indent) {
    out.indent(indent);
    out.print("BuiltinType [%p] %s\n", t, t.kind2str());
}
