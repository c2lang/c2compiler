/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

import string;

type SwitchCaseBits struct {
    u32 num_subcases: 8;
    u32 num_stmts : 10;
    u32 is_default : 1;
    u32 has_decls : 1;
}

public type SwitchCase struct @(opaque) {
    union {
        SwitchCaseBits bits;
        u32 allbits;
    }
    SrcLoc loc;
    union {
        Expr* cond;     // only for non-default
        IdentifierExpr*[1] multi_cond; // tail allocated
    }
    //Stmt*[0] stmts; // tail-allocated
}

// Note: cond can be nil in case of multi-subcases
public fn SwitchCase* SwitchCase.create(ast_context.Context* c,
                                        SrcLoc loc,
                                        bool is_default,
                                        Expr* cond,
                                        IdentifierExpr** multi,
                                        u32 num_multi,
                                        Stmt** stmts,
                                        u32 numStmts)
{
    assert(numStmts < 1024);    // 2^10
    assert(num_multi < 256);    // 2^8

    u32 size = sizeof(SwitchCase) + numStmts * sizeof(Stmt*);
    if (num_multi) {
        size += (num_multi - 1) * sizeof(IdentifierExpr*);
    }
    SwitchCase* s = c.alloc(size);
    s.allbits = 0;
    s.bits.is_default = is_default;
    s.bits.num_stmts = numStmts;
    s.bits.num_subcases = num_multi;
    s.loc = loc;
    if (num_multi) {
        string.memcpy(s.multi_cond, multi, num_multi * sizeof(IdentifierExpr*));
    } else {
        s.cond = cond;
    }

    Stmt** dst_stmts = s.getStmts();
    string.memcpy(cast<void*>(dst_stmts), cast<void*>(stmts), numStmts * sizeof(Stmt*));
#if AstStatistics
    Stats.addSwitchCase(size);
#endif
    return s;
}

fn SwitchCase* SwitchCase.instantiate(SwitchCase* s, Instantiator* inst) {
    u32 numStmts = s.getNumStmts();
    u32 size = sizeof(SwitchCase) + numStmts * sizeof(Stmt*);
    if (s.bits.num_subcases) {
        size += (s.bits.num_subcases - 1) * sizeof(IdentifierExpr*);
    }
    SwitchCase* s2 = inst.c.alloc(size);
    s2.allbits = s.allbits;
    s2.loc = s.loc;
    if (s.bits.num_subcases) {
        // just copy
        string.memcpy(s2.multi_cond, s.multi_cond, s.bits.num_subcases * sizeof(IdentifierExpr*));
    } else {
        // just copy?
        s2.cond = s.cond;
        //s2.cond = s.cond.instantiate(inst);
    }

    Stmt** src_stmts = s.getStmts();
    Stmt** dst_stmts = s.getStmts();
    for (u32 i=0; i<numStmts; i++) {
        dst_stmts[i] = src_stmts[i].instantiate(inst);
    }
#if AstStatistics
    Stats.addSwitchCase(size);
#endif
    return s;
}

public fn u32 SwitchCase.getNumStmts(const SwitchCase* s) {
    return s.bits.num_stmts;
}

public fn Stmt** SwitchCase.getStmts(const SwitchCase* s) {
    u8* stmts = cast<u8*>(&s.cond);
    if (s.bits.num_subcases) {
        stmts += s.bits.num_subcases * sizeof(void*);
    } else {
        stmts += sizeof(void*);
    }
    return cast<Stmt**>(stmts);
}

public fn bool SwitchCase.isDefault(const SwitchCase* s) { return s.bits.is_default; }

public fn u32 SwitchCase.numMulti(const SwitchCase* s) { return s.bits.num_subcases; }

public fn bool SwitchCase.hasDecls(const SwitchCase* s) { return s.bits.has_decls; }

public fn void SwitchCase.setHasDecls(SwitchCase* s) {
    s.bits.has_decls = 1;
}

public fn SrcLoc SwitchCase.getLoc(const SwitchCase* s) { return s.loc; }

public fn Expr* SwitchCase.getCond(const SwitchCase* s) { return s.cond; }

public fn Expr** SwitchCase.getCond2(SwitchCase* s) { return &s.cond; }

public fn IdentifierExpr** SwitchCase.getMultiCond(SwitchCase* s) { return s.multi_cond; }

fn void SwitchCase.print(const SwitchCase* s, string_buffer.Buf* out, u32 indent) {
    out.indent(indent);
    out.color(col_Stmt);
    out.add("SwitchCase");
    if (s.bits.is_default) {
        out.color(col_Attr);
        out.add(" default");
    }
    if (s.bits.has_decls) {
        out.color(col_Attr);
        out.add(" decls");
    }
    if (s.bits.num_subcases) {
        out.color(col_Attr);
        out.add(" multi");
    }
    out.newline();

    if (s.cond) {
        if (s.bits.num_subcases) {
            for (u32 i=0; i<s.bits.num_subcases; i++) {
                s.multi_cond[i].print(out, indent + 1);
            }
        } else {
            s.cond.print(out, indent + 1);
        }
    }

    Stmt** stmts = s.getStmts();
    for (u32 i=0; i<s.bits.num_stmts; i++) {
        stmts[i].print(out, indent + 1);
    }
}

