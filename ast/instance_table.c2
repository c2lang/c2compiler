/* Copyright 2022-2026 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import string;
import stdlib;

type TemplateInstance struct {
    QualType qt;
    Decl* instance;
}

type TemplateInstanceList struct {
    const Decl* fd;
    u16 count;
    u16 capacity;
    TemplateInstance* instances;
}

fn void TemplateInstanceList.init(TemplateInstanceList* f, const Decl* fd) {
    f.fd = fd;
    f.count = 0;
    f.capacity = 0;
    f.instances = nil;
    f.resize(2);
}

fn void TemplateInstanceList.resize(TemplateInstanceList* f, u16 capacity) {
    f.capacity = capacity;
    TemplateInstance* inst2 = stdlib.malloc(capacity* sizeof(TemplateInstance));
    if (f.count) {
        string.memcpy(inst2, f.instances, f.count * sizeof(TemplateInstance));
        stdlib.free(f.instances);
    }
    f.instances = inst2;
}

fn u16 TemplateInstanceList.add(TemplateInstanceList* f, QualType qt, Decl* instance) {
    if (f.count == f.capacity) f.resize(f.capacity * 2);

    u16 idx = f.count;
    TemplateInstance* ti = &f.instances[idx];
    f.count++;
    ti.qt = qt;
    ti.instance = instance;
    return idx;
}

fn Decl* TemplateInstanceList.find(const TemplateInstanceList* f, QualType qt) {
    for (u32 i=0; i<f.count; i++) {
        const TemplateInstance* ti = &f.instances[i];
        if (ti.qt.ptr == qt.ptr) return ti.instance;
    }
    return nil;
}

fn Decl* TemplateInstanceList.get(const TemplateInstanceList* f, u32 idx) @(unused) {
    return f.instances[idx].instance;
}

type InstanceTable struct {
    u32 count;
    u32 capacity;
    TemplateInstanceList* funcs;
}

fn void InstanceTable.init(InstanceTable* t) {
    t.count = 0;
    t.capacity = 0;
    t.funcs = nil;
}

fn void InstanceTable.free(InstanceTable* t) {
    for (u32 i=0; i<t.count; i++) {
        stdlib.free(t.funcs[i].instances);
    }
    stdlib.free(t.funcs);
}

fn void InstanceTable.resize(InstanceTable* t, u32 capacity) {
    t.capacity = capacity;
    TemplateInstanceList* funcs2 = stdlib.malloc(capacity * sizeof(TemplateInstanceList));
    if (t.count) {
        string.memcpy(funcs2, t.funcs, t.count * sizeof(TemplateInstanceList));
        stdlib.free(t.funcs);
    }
    t.funcs = funcs2;
}

fn TemplateInstanceList* InstanceTable.findFunc(const InstanceTable* t, const Decl* fd) {
    for (u32 i=0; i<t.count; i++) {
        TemplateInstanceList* fi = &t.funcs[i];
        if (fi.fd == fd) return fi;
    }
    return nil;
}

fn Decl* InstanceTable.find(const InstanceTable* t, const Decl* fd, QualType qt) {
    const TemplateInstanceList* fi = t.findFunc(fd);
    if (fi) return fi.find(qt);
    return nil;
}

fn Decl* InstanceTable.get(const InstanceTable* t, const Decl* fd, u32 idx) {
    const TemplateInstanceList* fi = t.findFunc(fd);
    assert(fi);
    return fi.get(idx);
}

fn u16 InstanceTable.add(InstanceTable* t, const Decl* fd, QualType qt, Decl* instance) {
    TemplateInstanceList* fi = t.findFunc(fd);
    if (!fi) {
        if (t.count == t.capacity) {
            if (t.capacity == 0) t.capacity = 2;
            t.resize(t.capacity * 2);
        }
        fi = &t.funcs[t.count];
        t.count++;
        fi.init(fd);
    }

    return fi.add(qt, instance);
}

/*
fn void InstanceTable.visit(const InstanceTable* t, const ast.Decl* fd, TemplateVisitor visitor, void* arg) {
    TemplateInstanceList* fi = t.findFunc(fd);
    if (!fi) return;
    for (u32 i=0; i<fi.count; i++) {
        visitor(arg, fi.instances[i].instance, i+1);
    }
}
*/

