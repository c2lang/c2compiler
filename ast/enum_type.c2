/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;

public type EnumType struct @(opaque) {
    Type base;
    EnumTypeDecl* decl;
}

fn EnumType* EnumType.create(ast_context.Context* c, EnumTypeDecl* decl) {
    EnumType* t = c.alloc(sizeof(EnumType));
    t.base.init(TypeKind.Enum);
    t.decl = decl;
#if AstStatistics
    Stats.addType(TypeKind.Enum, sizeof(EnumType));
#endif
    t.base.setCanonicalType(QualType.create(cast<Type*>(t)));
    return t;
}

public fn EnumTypeDecl* EnumType.getDecl(const EnumType* t) { return t.decl; }

//public fn Type* EnumType.asType(EnumType* t) { return &t.base; }

public fn QualType EnumType.getImplType(const EnumType* t) {
    return t.decl.getImplType();
}

// convenience function
public fn const char* EnumType.getName(const EnumType* t) {
    return t.decl.base.getName();
}

fn void EnumType.print(const EnumType* t, string_buffer.Buf* out) {
    out.add(t.decl.base.getModuleName());
    out.add1('.');
    out.add(t.decl.base.getName());
}

fn void EnumType.fullPrint(const EnumType* t, string_buffer.Buf* out, u32 indent) {
    out.indent(indent);
    out.print("EnumType [%p] %s\n", t, t.decl.asDecl().getName());
}
