/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import src_loc local;
import string_buffer;

import string;

type CompoundStmtBits struct {
    u32 : NumStmtBits;
    u32 count : 32 - NumStmtBits;
}

public type CompoundStmt struct {
    Stmt parent;
    SrcLoc endLoc;
    Stmt*[0] stmts; // tail-allocated
} @(opaque)

public fn CompoundStmt* CompoundStmt.create(ast_context.Context* c, SrcLoc end, Stmt** stmts, u32 count) {
    assert(count < 65556);
    u32 size = sizeof(CompoundStmt) + count * sizeof(Stmt*);

    CompoundStmt* s = c.alloc(size);
    s.parent.init(StmtKind.Compound);
    s.parent.compoundStmtBits.count = count;
    s.endLoc = end;

    if (count) {
        string.memcpy(cast<void*>(s.stmts), cast<void*>(stmts), count * sizeof(Stmt*));
    }
#if AstStatistics
    Stats.addStmt(StmtKind.Compound, size);
#endif
    return s;
}

public fn CompoundStmt* CompoundStmt.instantiate(CompoundStmt* s, Instantiator* inst) {
    const u32 count = s.parent.compoundStmtBits.count;
    u32 size = sizeof(CompoundStmt) + count * sizeof(Stmt*);

    CompoundStmt* s2 = inst.c.alloc(size);
    s2.parent = s.parent;
    s2.endLoc = s.endLoc;

    for (u32 i=0; i<count; i++) {
        s2.stmts[i] = s.stmts[i].instantiate(inst);
    }

    return s2;
}

public fn SrcLoc CompoundStmt.getEndLoc(const CompoundStmt* s) {
    return s.endLoc;
}

public fn u32 CompoundStmt.getCount(const CompoundStmt* s) {
    return s.parent.compoundStmtBits.count;
}

public fn Stmt** CompoundStmt.getStmts(CompoundStmt* s) {
    if (s.getCount()) return s.stmts;
    return nil;
}

public fn Stmt* CompoundStmt.getLastStmt(const CompoundStmt* s) {
    u32 count = s.getCount();
    if (count) return s.stmts[count-1];
    return nil;
}

fn void CompoundStmt.print(const CompoundStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    out.newline();
    const u32 count = s.parent.compoundStmtBits.count;
    for (u32 i=0; i<count; i++) {
        s.stmts[i].print(out, indent + 1);
    }
}


