/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import src_loc local;
import string_buffer;

import std;

type SwitchStmtBits struct {
    u32 : NumStmtBits;
    u32 is_string : 1;
    u32 has_default : 1;
    u32 num_cases : 32 - NumStmtBits - 2;
}

public type SwitchStmt struct @(opaque) {
    Stmt base;
    Expr* cond;
    SwitchCase*[0] cases; // tail-allocated
}

public fn SwitchStmt* SwitchStmt.create(ast_context.Context* c,
                                          SrcLoc loc,
                                          Expr* cond,
                                          SwitchCase** cases,
                                          u32 numCases,
                                          bool has_default)
{
    u32 size = sizeof(SwitchStmt) + numCases * sizeof(Stmt*);
    SwitchStmt* s = c.alloc(size);
    s.base.init(StmtKind.Switch, loc);
    s.base.switchStmtBits.has_default = has_default;
    s.base.switchStmtBits.num_cases = numCases;
    s.cond = cond;
    std.memcpy(s.cases, cases, numCases * sizeof(SwitchCase*));
#if AstStatistics
    Stats.addStmt(StmtKind.Switch, size);
#endif
    return s;
}

fn Stmt* SwitchStmt.instantiate(SwitchStmt* s, Instantiator* inst) {
    u32 numCases = s.getNumCases();
    u32 size = sizeof(SwitchStmt) + numCases * sizeof(SwitchCase*);
    SwitchStmt* s2 = inst.c.alloc(size);
    s2.base = s.base;
    s2.cond = s.cond.instantiate(inst);
    for (u32 i=0; i<numCases; i++) {
        s2.cases[i] = s.cases[i].instantiate(inst);
    }
#if AstStatistics
    Stats.addStmt(StmtKind.Switch, size);
#endif
    return (Stmt*)s2;
}

public fn Expr* SwitchStmt.getCond(const SwitchStmt* s) {
    return s.cond;
}

public fn Expr** SwitchStmt.getCond2(SwitchStmt* s) {
    return s.cond ? &s.cond : nil;
}

public fn bool SwitchStmt.isString(const SwitchStmt* s) {
    return s.base.switchStmtBits.is_string;
}

public fn void SwitchStmt.setString(SwitchStmt* s) {
    s.base.switchStmtBits.is_string = true;
}

public fn bool SwitchStmt.hasDefault(const SwitchStmt* s) {
    return s.base.switchStmtBits.has_default;
}

public fn u32 SwitchStmt.getNumCases(const SwitchStmt* s) {
    return s.base.switchStmtBits.num_cases;
}

public fn SwitchCase** SwitchStmt.getCases(SwitchStmt* s) {
    return s.cases;
}

fn void SwitchStmt.print(const SwitchStmt* s, string_buffer.Buf* out, u32 indent) {
    s.base.printKind(out, indent);
    if (s.isString()) {
        out.color(col_Attr);
        out.add(" string");
    }
    if (s.hasDefault()) {
        out.color(col_Attr);
        out.add(" has_default");
    }
    out.newline();

    s.cond.print(out, indent + 1);
    for (u32 i=0; i<s.base.switchStmtBits.num_cases; i++) {
        s.cases[i].print(out, indent + 1);
    }
}

