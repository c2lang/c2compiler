/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import src_loc local;
import string_buffer;

public type ForStmt struct @(opaque) {
    Stmt base;
    Stmt* init;
    Expr* cond;
    Expr* cont;
    Stmt* body;
}

public fn ForStmt* ForStmt.create(ast_context.Context* c, SrcLoc loc, Stmt* init, Expr* cond, Expr* cont, Stmt* body) {
    ForStmt* s = c.alloc(sizeof(ForStmt));
    s.base.init(StmtKind.For, loc);
    s.init = init;
    s.cond = cond;
    s.cont = cont;
    s.body = body;
#if AstStatistics
    Stats.addStmt(StmtKind.For, sizeof(ForStmt));
#endif
    return s;
}

fn Stmt* ForStmt.instantiate(ForStmt* s, Instantiator* inst) {
    Stmt* init2 = s.init ? s.init.instantiate(inst) : nil;
    Expr* cond2 = s.cond ? s.cond.instantiate(inst) : nil;
    Expr* cont2 = s.cont ? s.cont.instantiate(inst) : nil;
    Stmt* body2 = s.body ? s.body.instantiate(inst) : nil;
    return cast<Stmt*>(ForStmt.create(inst.c, s.base.loc, init2, cond2, cont2, body2));
}

public fn Stmt* ForStmt.getInit(const ForStmt* s) { return s.init; }
public fn Expr* ForStmt.getCond(const ForStmt* s) { return s.cond; }
public fn Expr* ForStmt.getCont(const ForStmt* s) { return s.cont; }
public fn Stmt* ForStmt.getBody(const ForStmt* s) { return s.body; }

public fn Stmt** ForStmt.getInit2(ForStmt* s) { return s.init ? &s.init : nil; }
public fn Expr** ForStmt.getCond2(ForStmt* s) { return s.cond ? &s.cond : nil; }
public fn Expr** ForStmt.getCont2(ForStmt* s) { return s.cont ? &s.cont : nil; }
//public fn Stmt** ForStmt.getBody2(ForStmt* s) { return s.body ? &s.body : nil; }

fn void ForStmt.print(const ForStmt* s, string_buffer.Buf* out, u32 indent) {
    s.base.printKind(out, indent);
    out.newline();

    if (s.init) s.init.print(out, indent + 1);
    if (s.cond) s.cond.print(out, indent + 1);
    if (s.cont) s.cont.print(out, indent + 1);
    if (s.body) s.body.print(out, indent + 1);
}

