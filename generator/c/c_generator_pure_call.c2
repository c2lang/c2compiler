/* Copyright 2022-2025 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c_generator;

import ast local;
import ctv_analyser;
import string_buffer;

type ArgValue struct {
    u32 name;
    Value value;
}

type Evaluator struct {
    ArgValue[16] values; // TODO use constants.MaxArgs or something
    u32 num_values;
    Value result;
}

fn void Evaluator.check(Evaluator* eval, FunctionDecl* fd, u32 num, Expr** args) {
    eval.result.setUnsigned(0);
    eval.num_values = num;

    VarDecl** params = fd.getParams();

    for (u32 i=0; i<num; i++) {
        Decl* d = cast<Decl*>(params[i]);
        ArgValue* v = &eval.values[i];
        v.name = d.getNameIdx();
        v.value = ctv_analyser.get_value(args[i]);
    }
    CompoundStmt* body = fd.getBody();
    u32 count = body.getCount();
    Stmt** stmts = body.getStmts();
    assert(count == 1);
    assert(stmts[0].isReturn());
    ReturnStmt* r = cast<ReturnStmt*>(stmts[0]);
    eval.result = eval.get_value(r.getValue());
}

// TODO duplicate code with ctv_analyser!
fn Value Evaluator.get_value(Evaluator* eval, const Expr* e) {
    Value result = { }

    switch (e.getKind()) {
    case IntegerLiteral:
        const IntegerLiteral* i = cast<IntegerLiteral*>(e);
        if (i.isSigned())
            result.setSigned(cast<i64>(i.getValue()));
        else
            result.setUnsigned(i.getValue());
        break;
    case FloatLiteral:
        const FloatLiteral* f = cast<FloatLiteral*>(e);
        result.setFloat(f.getValue());
        break;
    case BooleanLiteral:
        const BooleanLiteral* b = cast<BooleanLiteral*>(e);
        result.setUnsigned(b.getValue());
        break;
    case CharLiteral:
        const CharLiteral* c = cast<CharLiteral*>(e);
        result.setUnsigned(cast<u8>(c.getValue()));
        break;
    case StringLiteral:
        assert(0);
        break;
    case Nil:
        break;
    case Identifier:
        const IdentifierExpr* i = cast<IdentifierExpr*>(e);
        return eval.get_decl_value(i.getDecl());
    case Type:
    case Call:
    case InitList:
    case FieldDesignatedInit:
    case ArrayDesignatedInit:
        // should not happen
        break;
    case BinaryOperator:
        return eval.get_binaryop_value(cast<BinaryOperator*>(e));
    case UnaryOperator:
        return eval.get_unaryop_value(cast<UnaryOperator*>(e));
    case ConditionalOperator:
        ConditionalOperator* c = cast<ConditionalOperator*>(e);
        Value v = eval.get_value(c.getCond());
        return !v.isZero() ? eval.get_value(c.getLHS()) : eval.get_value(c.getRHS());
    case Builtin:
        const BuiltinExpr* bi = cast<BuiltinExpr*>(e);
        result = bi.getValue();
        break;
    case ArraySubscript:
        // note: can be CTV if BitOffsetExpr
        ArraySubscriptExpr* a = cast<ArraySubscriptExpr*>(e);

        result = eval.get_value(a.getBase());
        // Dont allow negative/float
        assert(result.isDecimal() && !result.isNegative());

        Expr* index = a.getIndex();
        assert(index.isBitOffset());
        BitOffsetExpr* bo = cast<BitOffsetExpr*>(index);
        Value high = eval.get_value(bo.getLHS());
        Value low = eval.get_value(bo.getRHS());
        Value width = high.minus(&low);
        width.incr();
        // calculate result = (base >> low) & bitmask(width))
        result = result.right_shift(&low);
        result.mask(width.as_u32());
        break;
    case Member:
        const MemberExpr* m = cast<MemberExpr*>(e);
        return eval.get_decl_value(m.getFullDecl());
    case Paren:
        const ParenExpr* p = cast<ParenExpr*>(e);
        return eval.get_value(p.getInner());
    case BitOffset:
        // TODO
        break;
    case ExplicitCast:
        assert(0); // TODO
#if 0
        const ExplicitCastExpr* i = cast<ExplicitCastExpr*>(e);
        result = eval.get_value(i.getInner());
        QualType qt = e.getType();
        qt = qt.getCanonicalType();
        assert(qt.isBuiltin());
        BuiltinType* bi = qt.getBuiltin();
        result.truncate(bi.isSigned(), bi.getWidth());
#endif
        break;
    case ImplicitCast:
        const ImplicitCastExpr* i = cast<ImplicitCastExpr*>(e);
        return eval.get_value(i.getInner());
    case Range:
        assert(0);
        break;
    }

    return result;
}

fn Value Evaluator.get_binaryop_value(Evaluator* eval, const BinaryOperator* e) {
    Value result = { }
    Value left = eval.get_value(e.getLHS());
    Value right = eval.get_value(e.getRHS());

    // Note: we take the signed/unsigned rvalue depending on whether the lhs value is sigend.
    // this is not completely correct

    switch (e.getOpcode()) {
    case Multiply:
        result = left.multiply(&right);
        break;
    case Divide:
        result = left.divide(&right);
        break;
    case Remainder:
        result = left.remainder(&right);
        break;
    case Add:
        result = left.add(&right);
        break;
    case Subtract:
        result = left.minus(&right);
        break;
    case ShiftLeft:
        // Note: neither may be negative, should already be checked
        // TODO warn about overflow
        // TODO fix
        result = left.left_shift(&right);
        break;
    case ShiftRight:
        // Note: neither may be negative, should already be checked
        // Note: rhs value should be less than bitwidth of lhs type, should already be checked
        // TODO fix
        result = left.right_shift(&right);
        break;
    case LessThan:
        result.setUnsigned(left.is_less(&right));
        break;
    case GreaterThan:
        result.setUnsigned(left.is_greater(&right));
        break;
    case LessEqual:
        result.setUnsigned(left.is_less_equal(&right));
        break;
    case GreaterEqual:
        result.setUnsigned(left.is_greater_equal(&right));
        break;
    case Equal:
        result.setUnsigned(left.is_equal(&right));
        break;
    case NotEqual:
        result.setUnsigned(!left.is_equal(&right));
        break;
    case And:
        result = left.and(&right);
        break;
    case Xor:
        result = left.xor(&right);
        break;
    case Or:
        result = left.or(&right);
        break;
    case LAnd:
        result = left.land(&right);
        break;
    case LOr:
        result = left.lor(&right);
        break;
    case Assign:
    case MulAssign:
    case DivAssign:
    case RemAssign:
    case AddAssign:
    case SubAssign:
    case ShlAssign:
    case ShrAssign:
    case AndAssign:
    case XorAssign:
    case OrAssign:
        assert(0);
        break;
    }
    return result;
}

fn Value Evaluator.get_unaryop_value(Evaluator* eval, const UnaryOperator* e) {
    Value result = { }
    const Expr* inner = e.getInner();
    Value res2 = eval.get_value(inner);

    switch (e.getOpcode()) {
    case PostInc:
    case PostDec:
    case PreInc:
    case PreDec:
        // should not happen
        break;
    case AddrOf:
    case Deref:
        // Allowed?
        break;
    case Plus:
        result = res2;
        break;
    case Minus:
        result = res2.negate();
        break;
    case Not:
        assert(res2.isDecimal()); // TODO give error
        result = res2.bitnot();
        break;
    case LNot:
        assert(res2.isDecimal()); // TODO give error
        result = res2.lnot();
        break;
    }
    return result;
}

fn Value Evaluator.get_decl_value(Evaluator* eval, const Decl* d) {
    assert(d);
    Value result;
    switch (d.getKind()) {
    case EnumConstant:
        const EnumConstantDecl* ecd = cast<EnumConstantDecl*>(d);
        result = ecd.getValue();
        break;
    case Variable:
        const u32 name_idx = d.getNameIdx();
        VarDecl* vd = cast<VarDecl*>(d);
        VarDeclKind vk = vd.getKind();
        if (vk == VarDeclKind.FunctionParam) {
            for (u32 i=0; i<eval.num_values; i++) {
                ArgValue* v = &eval.values[i];
                if (v.name == name_idx) {
                    return v.value;
                }
            }
            assert(0);
        } else {
            const Expr* initval = vd.getInit();
            assert(initval);
            return eval.get_value(initval);
        }
        break;
    default:
        assert(0);
        break;
    }
    return result;
}

fn void Generator.emitPureFunctionCall(Generator* gen, string_buffer.Buf* out, Expr* e) {
    // evaluate and emit number
    CallExpr* c = cast<CallExpr*>(e);
    u32 num_args = c.getNumArgs();
    Expr** args = c.getArgs();

    //Expr* func = c.getFunc();
    FunctionDecl* fd = expr2function(c.getFunc());
    // Note: numParams will be the same as num_args

    Evaluator eval;
    eval.check(fd, num_args, args);
    // TODO dont copy twice (just print to string-buffer
    out.add(eval.result.str());
}

fn FunctionDecl* expr2function(Expr* e) {
    switch (e.getKind()) {
    case Identifier:
        IdentifierExpr* i = cast<IdentifierExpr*>(e);
        return cast<FunctionDecl*>(i.getDecl());
    case Member:
        MemberExpr* m = cast<MemberExpr*>(e);
        return cast<FunctionDecl*>(m.getFullDecl());
    case ImplicitCast:
        ImplicitCastExpr* ic = cast<ImplicitCastExpr*>(e);
        return expr2function(ic.getInner());
    default:
        assert(0);
        break;
    }
    return nil;
}

